plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.11.0'
}

group 'com.rundeck'

ext.rundeckPluginVersion = '1.2'
ext.pluginClassNames='com.rundeck.plugin.GitResourceModelFactory'
ext.pluginName = 'Git Resource Model'
ext.pluginDescription = 'Writable Git Resource Model'

//sourceCompatibility = 1.8

scmVersion {
    ignoreUncommittedChanges = true
    tag {
        prefix = ''
        versionSeparator = ''
        def origDeserialize=deserialize
        //apend .0 to satisfy semver if the tag version is only X.Y
        deserialize = { config, position, tagName ->
            def orig = origDeserialize(config, position, tagName)
            if (orig.split('\\.').length < 3) {
                orig += ".0"
            }
            orig
        }
    }
}
project.version = scmVersion.version

apply plugin: 'groovy'
apply plugin: 'java'

repositories {
    mavenCentral()
}

configurations {
    pluginLibs

    compile {
        extendsFrom pluginLibs
    }
}


dependencies {
    compile 'org.codehaus.groovy:groovy-all:3.0.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'

    compile group: 'org.rundeck', name: 'rundeck-core', version: '4.14.0-20230615'
    compile group: 'org.rundeck', name: 'rundeck-storage-api', version: '4.14.0-20230615'
    compile 'org.slf4j:slf4j-api:2.0.7'

    pluginLibs( 'org.eclipse.jgit:org.eclipse.jgit:6.6.0.202305301015-r') {
        exclude module: 'slf4j-api'
        exclude module: 'jsch'
        exclude module: 'commons-logging'
    }

    pluginLibs( 'org.eclipse.jgit:org.eclipse.jgit.ssh.jsch:6.6.0.202305301015-r') {
        exclude module: 'slf4j-api'
        exclude module: 'commons-logging'
    }

    testCompile "org.codehaus.groovy:groovy-all:3.0.0"
    testCompile "org.spockframework:spock-core:2.4-M1-groovy-3.0"
    testCompile "cglib:cglib-nodep:2.2.2"
    testCompile 'org.objenesis:objenesis:1.4'
}


task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        def libList = configurations.pluginLibs.collect{'lib/' + it.name}.join(' ')
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-File-Version': project.version
        attributes 'Rundeck-Plugin-Version': rundeckPluginVersion
        attributes 'Rundeck-Plugin-Archive': 'true'
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
    dependsOn(copyToLib)
}


task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}

